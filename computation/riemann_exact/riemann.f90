program riemann

  ! an exact Riemann solver for the Euler equations, from Toro Ch. 4

  implicit none

  integer :: i, cells

  double precision :: gamma, g1, g2, g3, g4, g5, g6, g7, g8
  double precision :: dl, ul, pl, cl, dr, ur, pr, cr
  double precision :: diaph, domlen, ds, dx, pm, mpa, ps, s
  double precision :: timeout, um, us, xpos

  common /gammas/ gamma,g1, g2, g3, g4, g5, g6, g7, g8
  common /states/ dl, ul, pl, cl, dr, ur, pr, cr

  ! open the input file and read in the parameters
  open (unit = 1, file = 'exact.ini', status = 'unknown')

  read (1,*) domlen   ! domain length
  read (1,*) diaph    ! discontinuity position
  read (1,*) cells    ! number of computing cells
  read (1,*) gamma    ! ratio of specific heats
  read (1,*) timeout  ! output time
  read (1,*) dl       ! initial density on left state
  read (1,*) ul       ! initial velocity on left state
  read (1,*) pl       ! initial pressure on left state
  read (1,*) dr       ! initial density on right state
  read (1,*) ur       ! initial velocity on right state
  read (1,*) pr       ! initial pressure on right state
  read (1,*) mpa      ! normalizing constant

  close (1)

  ! compute the gamma related constants
  g1 = (gamma - 1.0)/(2.0*gamma)
  g2 = (gamma + 1.0)/(2.0*gamma)
  g3 = 2.0*gamma/(gamma - 1.0)
  g4 = 2.0/(gamma - 1.0)
  g5 = 2.0/(gamma + 1.0)
  g6 = (gamma - 1.0)/(gamma + 1.0)
  g7 = (gamma - 1.0)/2.0
  g8 = gamma - 1.0

  ! compute the sound speeds
  cl = sqrt(gamma*pl/dl)
  cr = sqrt(gamma*pr/dr)

  ! the pressure positivity condition is tested for 
  if (g4*(cl+cr) < (ur-ul)) then
     
     ! the initial data is such that a vacuum is generated
     write (6,*)
     write (6,*) 'Error: Vacuum is generated by data'
     stop
  endif

  ! exact solution for pressure and velocity in star region is found
  call starpu(pm, um, mpa)

  dx = domlen/real(cells)

  ! compute solution at time TIMEOUT is found
  open (unit = 2, file = 'exact.out', status = 'unknown')

  do i = 1, cells

     xpos = (dble(i) - 0.5)*dx
     s = (xpos - diaph)/timeout

     ! solution at point (x,t) = (xpos - diaph, timeout) is found
     call sample (pm, um, s, ds, us, ps)

     ! exact solution profiles are written to exact.out
     write (2, 20) xpos, ds, us, ps/mpa, ps/ds/g8/mpa

  enddo

  close (2)

20 format(5(f14.6, 2x))

end program riemann



subroutine starpu(p, u, mpa)

  implicit none

  ! compute the solution for pressure and velocity in the star region
  
  integer :: i, nriter

  double precision :: dl, ul, pl, cl, dr, ur, pr, cr
  double precision :: change, fl, fld, fr, frd, p, pold, pstart
  double precision :: tolpre, u, udiff, mpa

  logical :: converged

  common /states/ dl, ul, pl, cl, dr, ur, pr, cr
  data tolpre, nriter /1.0d-6, 20/

  ! guessed value pstart is computed
  call guessp(pstart)

  pold = pstart
  udiff = ur - ul

  write (*,*) 'iternation number, change'

  converged = .false.

  do i = 1, nriter

     call prefun(fl, fld, pold, dl, pl, cl)
     call prefun(fr, frd, pold, dr, pr, cr)

     p = pold - (fl + fr + udiff)/(fld + frd)
     change = 2.0*abs((p - pold)/(p + pold))

     write (*,*) i, change
     
     if (change <= tolpre) then
        converged = .true.
        exit
     endif

     if (p < 0.0) p = tolpre

     pold = p

  enddo

  if (.not. converged) then
     print *, 'ERROR: divergence in Newton-Raphson iteration'
  endif


  ! compute the velocity in the star region
  u = 0.5*(ul + ur + fr - fl)

  print *, 'pressure, velocity'
  print *, p/mpa, u
  
  return
end subroutine starpu
   


subroutine guessp(pm)

  ! provide a guess value for the pressure PM in the star region
  ! according to the adaptive Riemann solver using the PVRS,
  ! TRRS and TSRS approximate Riemann solvers, as in Toro 9.5

  implicit none

  double precision :: dl, ul, pl, cl, dr, ur, pr, cr
  double precision :: gamma, g1, g2, g3, g4, g5, g6, g7, g8
  double precision :: cup, gel, ger, pm, pmax, pmin, ppv, pq, &
       ptl, ptr, qmax, quser, um

  common /gammas/ gamma, g1, g2, g3, g4, g5, g6, g7, g8
  common /states/ dl, ul, pl, cl, dr, ur, pr, cr

  quser = 2.0

  ! compute guess pressure from PVRS Riemann solver
  cup = 0.25*(dl + dr)*(cl + cr)

  ppv = 0.5*(pl + pr) + 0.5*(ul - ur)*cup
  ppv = max(0.0, ppv)

  pmin = min(pl, pr)
  pmax = max(pl, pr)
  
  qmax = pmax/pmin

  if (qmax <= quser .and. (pmin <= ppv .and. ppv <= pmax)) then
     
     ! PVRS Riemann solver
     pm = ppv
     
  else

     if (ppv < pmin) then

        ! two-rarefaction Riemann solver
        pq = (pl/pr)**g1
        um = (pq*ul/cl + ur/cr + g4*(pq - 1.0))/(pq/cl + 1.0/cr)
        ptl = 1.0 + g7*(ul - um)/cl
        ptr = 1.0 + g7*(um - ur)/cr
        pm = 0.5*(pl*ptl**g3 + pr*ptr**g3)
        
     else

        ! select the two-shock Riemann solver
        gel = sqrt((g5/dl)/(g6*pl + ppv))
        ger = sqrt((g5/dr)/(g6*pr + ppv))

        pm = (gel*pl + ger*pr - (ur-ul))/(gel + ger)
        
     endif
  endif

  return
end subroutine guessp

  
  
subroutine prefun(f, fd, p, dk, pk, ck)

  ! evaluate the pressure functions fl and fr in exact Riemann solver

  implicit none

  double precision :: ak, bk, ck, dk, f, fd, p, pk, prat, qrt
  double precision :: gamma, g1, g2, g3, g4, g5, g6, g7, g8

  common /gammas/ gamma, g1, g2, g3, g4, g5, g6, g7, g8

  if (p <= pk) then
     
     ! rarefaction wave
     prat = p/pk
     f = g4*ck*(prat**g1- 1.0)
     fd = (1.0/(dk*ck))*prat**(-g2)
  else

     ! shock wave
     ak = g5/dk
     bk = g6*pk
     qrt = sqrt(ak/(bk + p))
     f = (p - pk)*qrt
     fd = (1.0 - 0.5*(p - pk)/(bk + p))*qrt
  endif

  return
end subroutine prefun



subroutine sample(pm, um, s, d, u, p)

  ! sampe the solution thoughout the wave patern.  Pressure PM and
  ! velocity UM in the star region are known.  Sampling is performed
  ! in terms of the 'speed' S = X/T.  Sampled values are d, u, p.

  implicit none

  double precision :: dl, ul, pl, cl, dr, ur, pr, cr
  double precision :: gamma, g1, g2, g3, g4, g5, g6, g7, g8
  double precision :: c, cml, cmr, d, p, pm, pml,pmr, s
  double precision :: shl, shr, sl, sr, stl, str, u, um

  common /gammas/ gamma, g1, g2, g3, g4, g5, g6, g7, g8
  common /states/ dl, ul, pl, cl, dr, ur, pr, cr

  if (s <= um) then
     
     ! sampling point lies to the left of the contact discontinuity

     if (pm <= pl) then

        ! left rarefaction

        shl = ul - cl

        if (s .le. shl) then

           ! sampled point is left data state
           d = dl
           u = ul
           p = pl
           
        else
           cml = cl*(pm/pl)**g1
           stl = um - cml

           if (s > stl) then

              ! sampled point is in the star left state
              d = dl*(pm/pl)**(1.0/gamma)
              u = um
              p = pm

          else

             ! sampled point is inside the left fan
             u = g5*(cl + g7*ul + s)
             c = g5*(cl + g7*(ul - s))
             d = dl*(c/cl)**g4
             p = pl*(c/cl)**g3

          endif

       endif

    else

       ! left shock
       pml = pm/pl
       sl = ul - cl*sqrt(g2*pml + g1)

       if (s <= sl) then

          ! sampled point is left data state
          d = dl
          u = ul
          p = pl

       else

          ! sampled point is star left state
          d = dl*(pml + g6)/(pml*g6 + 1.0)
          u = um
          p = pm
       endif

    endif
    
 else

    ! sampling lies to the right of the contact discontinuity
    
    if (pm > pr) then

       ! right shock
       pmr = pm/pr
       sr = ur + cr*sqrt(g2*pmr + g1)

       if (s >= sr) then

          ! sample point is right data state
          d = dr
          u = ur
          p = pr

       else

          ! sampled point is star right state
          d = dr*(pmr + g6)/(pmr*g6 + 1.0)
          u = um
          p = pm
       endif
    else 

       ! right rarefaction
       shr = ur + cr

       if (s >= shr) then
          
          ! sampled point is right data state
          d = dr
          u = ur
          p = pr
       else
          cmr = cr*(pm/pr)**g1
          str = um + cmr
          
          if (s <= str) then
             
             ! sampled point is star right state
             d = dr*(pm/pr)**(1.0/gamma)
             u = um
             p = pm
          else
             
             ! sampled point is inside left fan
             u = g5*(-cr + g7*ur + s)
             c = g5*(cr - g7*(ur - s))
             d = dr*(c/cr)**g4
             p = pr*(c/cr)**g3
          endif
       endif
    endif
 endif


 return
end subroutine sample
  
